apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def getVersionCode = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'sh', '-c', "git describe --tags --always | sed 's/-\\([0-9]\\+\\)-g/-/'"
        standardOutput = stdout
    }
    def versionName = stdout.toString().trim()
    if (versionName.startsWith("v")) {
        return versionName.substring(1)
    }
    return versionName
}

android {
    namespace "de.meonwax.soundboard"
    compileSdkVersion 34

    defaultConfig {
        applicationId "de.meonwax.soundboard"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode getVersionCode()
        versionName getVersionName()
    }
    signingConfigs {
        release {
            // @See gradle.properties
            storeFile file(releaseKeyStore)
            storePassword releaseKeyStorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyAliasPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
}
